#!/usr/bin/perl
use strict;
use warnings;

#
# bin/attn - Attention grabbing grep replacement
#
# author:  James Hunt <james@niftylogic.com>
# created: 2015-02-12
#
# USAGE: tail -f $logfile | cgrep . red=FATAL blue='(of note|interesting|skipped)'
#

my $filter = shift @ARGV;
my @PATTERNS = map { [split "=", $_, 2] } @ARGV;
my %COLORS = (
	black   => "\x1b[30;1m",
	red     => "\x1b[31;1m",
	green   => "\x1b[32;1m",
	yellow  => "\x1b[33;1m",
	blue    => "\x1b[34;1m",
	magenta => "\x1b[35;1m",
	cyan    => "\x1b[36;1m",
	white   => "\x1b[37;1m",
	reset   => "\x1b[0m",
);

select STDOUT; $| = 1;
LINE:
while (<STDIN>) {
	chomp;
	next unless m/$filter/;

	for my $p (@PATTERNS) {
		my ($color, $pattern) = @$p;

		if (m/$pattern/) {
			print "$COLORS{$color}$_$COLORS{reset}\n";
			next LINE;
		}
	}
	print "$COLORS{black}$_$COLORS{reset}\n";
}
