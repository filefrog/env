#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long qw/:config bundling/;
use Pod::Usage;

my %OPTIONS = (
	name     => 'default',
	network  => '10.244.2.x',
	count    => 8,
	expanded => 0,
);
GetOptions(\%OPTIONS, qw/
	help|h|?

	name|N=s
	network|n=s
	count|c=i
	expanded|e
/) or pod2usage(2);
pod2usage(1) if $OPTIONS{help};
pod2usage('Bad --network value')
	unless $OPTIONS{network} =~ m/^[.0-9x]+$/;
pod2usage('--network should only specify a single octet as "x"')
	if (($OPTIONS{network} =~ m/x/) > 1);
pod2usage('--network must specify an octet as "x"')
	unless $OPTIONS{network} =~ s/x/%d/;
pod2usage('--count is too high (must be <= 64)')
	if $OPTIONS{count} > 64;
pod2usage('--count is too low (must be > 0)')
	if $OPTIONS{Pcount} < 1;

print <<EOF;
networks:
  - name: $OPTIONS{name}
    type: manual
    subnets:
EOF

my $n = 0;
for (my $i = 0; $i < $OPTIONS{count}; $i++) {
	my $range    = sprintf($OPTIONS{network}, $n);
	my $reserved = sprintf($OPTIONS{network}, $n + 1);
	my $static   = sprintf($OPTIONS{network}, $n + 2);
	$n += 4;

	if ($OPTIONS{expanded}) {
		print <<EOF;
      - range:     $range/30
        reserved: [$reserved]
        static:   [$static]
EOF
	} else {
		printf "      - { range: %s, reserved: [%s], static: [%s] }\n", "$range/30", $reserved, $static;
	}
}

=head1 NAME

warden-nets - Create BOSH Network Definitions for Warden CPI

=head1 SYNOPSIS

warden-nets [-N <name>] [-n 10.244.2.x] [-c <count>] [--expanded]

=head1 DESCRIPTION

The BOSH Warden Cloud Provider Interface allocates all networks as /30, with
2 usable IP addresses per network.  Defining these by hand is tedius, and
just begs to be automated.

=head1 OPTIONS

=over

=item B<-N>, B<--name> I<NAME>

The name of the network.  Defaults to C<default>.

=item B<-n>, B<--network> I<NETWORK>

A network spec, with a single octet replaced by the literal character 'x'.
This placeholder value will be replaced by the correct values for each
successive /30 network defined (per B<--count>).

=item B<-c>, B<--count> I<NUMBER>

How many networks to define, between 1 and 64.  Defaults to C<8>.

=item B<-e>, B<--expanded>

Print network YAML definitions in expanded form (block YAML, not flow).
Useful if you intend to add custom attributes (like 'dns') to the output.

=back

=head1 AUTHOR

Written by James Hunt B<< <james@jameshunt.us > >>

=cut
